import org.gradle.plugins.ide.eclipse.model.AccessRule

import java.text.DateFormat
import java.time.ZoneId
import java.time.ZoneOffset

plugins {
	id "edu.sc.seis.macAppBundle" version "2.2.1"
	id "org.sonarqube" version "2.6.2"
}

apply plugin: 'application'
apply plugin: 'eclipse'

sourceCompatibility="1.8"
targetCompatibility="1.8"

repositories {
	mavenLocal()
	jcenter()
	maven {
		name "sonatype snapshots"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
	maven {
		name "sonatype release"
		url "https://oss.sonatype.org/content/repositories/releases"
	}
}

mainClassName = "de.prob2.ui.ProB2"
final preloaderClassName = "de.prob2.ui.ProB2Preloader"
applicationDefaultJvmArgs += ["-Djavafx.preloader=${preloaderClassName}"]

macAppBundle {
	dmgName = project.name + "-macos"
	icon = "prob2.icns"
	bundleJRE = true
	javaProperties = ["javafx.preloader": preloaderClassName]
}

macAppBundle.mainClassName = mainClassName

if (hasProperty("macAppJRE")) {
	// Used to set a custom JRE to bundle at the command line: -PmacAppJRE=/path/to/the/jre
	macAppBundle.jreHome = macAppJRE
}

sourceSets.test.java.srcDirs = []

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

dependencies {
	compile 'com.google.code.gson:gson:2.8.2'
	compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
	compile 'de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.6'
	compile group: 'de.hhu.stups', name: 'de.prob2.kernel', version: '3.2.10-SNAPSHOT', changing: true
	compile group: 'de.hhu.stups', name: 'ltl-dsl', version: '0.1.1-SNAPSHOT', changing: true
	compile 'de.jensd:fontawesomefx-commons:8.15'
	compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-5'
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.9.0'
	compile 'org.fxmisc.wellbehaved:wellbehavedfx:0.3.3'
	compile 'org.hildan.fxgson:fx-gson:3.1.0'
	compile 'se.sawano.java:alphanumeric-comparator:1.4.1'
	compile 'org.pf4j:pf4j:2.2.0'

	testCompile 'org.loadui:testFx:3.1.2'
	testCompile 'org.testfx:testfx-core:4.0.+'
	testCompile 'org.testfx:testfx-junit:4.0.+'
	testRuntime 'org.testfx:openjfx-monocle:1.8.0_20'
}

eclipse.classpath.file.whenMerged {
	classpath ->
		classpath.entries.findResult { entry ->
			if (entry.kind == 'con' && entry.path.contains('org.eclipse.jdt.launching.JRE_CONTAINER')) {
					entry.accessRules.add(new AccessRule('accessible', 'javafx/**'))
					entry.accessRules.add(new AccessRule('accessible', 'netscape/**'))
			}
		}
}

processResources {
	doFirst {
		def f = file("src/main/resources/de/prob2/ui/build.properties")
		f.delete()
		def dateFormat = DateFormat.getDateTimeInstance(DateFormat.FULL, DateFormat.FULL, Locale.ROOT)
		dateFormat.timeZone = TimeZone.getTimeZone(ZoneId.from(ZoneOffset.UTC))
		f.append("buildTime=${dateFormat.format(new Date())}\n")
		def proc = new ProcessBuilder("git", "rev-parse", "HEAD").directory(buildDir).start()
		def exitCode = proc.waitFor()
		if (exitCode != 0) {
			throw new IllegalStateException("Command exited with status code ${exitCode}:\n" + proc.err.readLines().join("\n"))
		}
		def hash = proc.in.readLines()[0]
		f.append("commit=${hash}\n")
	}
}

sonarqube {
	properties {
		property("sonar.java.binaries", sourceSets.main.java.outputDir.path)
		
		property("sonar.projectKey", "prob2-ui")
		property("sonar.projectName", "prob2-ui")
		
		property("sonar.exclusions", "gradle/wrapper/*,build/**,scrapbook/*,src/main/resources/codemirror/clike.js,src/main/resources/codemirror/closebrackets.js,src/main/resources/codemirror/codemirror.css,src/main/resources/codemirror/codemirror.js,src/main/resources/codemirror/jquery.js,src/main/resources/codemirror/matchbrackets.js")
		
		property("sonar.login", System.getenv("SONAR_TOKEN"))
	}
}

task wrapper(type: Wrapper) {
	distributionType = "ALL"
	gradleVersion = "4.7"
}

task createHelp(type: Exec) {
	inputs.dir("src/main/resources/help")
	outputs.dir("src/main/resources/help")
	commandLine './help.sh'
}
processResources.dependsOn(createHelp)

allprojects {
	final probHome = System.getenv("PROB_HOME")
	if (probHome != null) {
		tasks.withType(JavaExec) {
			systemProperties["prob.home"] = probHome
		}
	}
}
